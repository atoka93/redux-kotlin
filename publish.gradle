apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

version '1.2.0'
group = 'net.attilaszabo.redux'
archivesBaseName = BINTRAY_PACKAGE_NAME

if (project.hasProperty("android")) {
    publishing {
        publications {
            AndroidLibrary(MavenPublication) {
                artifact("$buildDir/outputs/aar/$archivesBaseName-release.aar")
                groupId this.group
                artifactId this.archivesBaseName
                version this.version

                pom.withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')
                    // Iterate over the implementation dependencies (we don't want the test ones), adding a <dependency> node for each
                    configurations.implementation.allDependencies.each {
                        // Ensure dependencies such as fileTree are not included in the pom.
                        if (it.name.startsWith("redux-")) {
                            def dependencyNode = dependenciesNode.appendNode('dependency')
                            dependencyNode.appendNode('groupId', it.group)
                            dependencyNode.appendNode('artifactId', it.name.replace("redux-", "redux-kotlin-"))
                            dependencyNode.appendNode('version', it.version)
                            dependencyNode.appendNode('scope', 'compile')
                        } else if (it.name != 'unspecified') {
                            def dependencyNode = dependenciesNode.appendNode('dependency')
                            dependencyNode.appendNode('groupId', it.group)
                            dependencyNode.appendNode('artifactId', it.name)
                            dependencyNode.appendNode('version', it.version)
                            dependencyNode.appendNode('scope', 'runtime')
                        }
                    }
                }
            }
        }
    }

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from android.sourceSets.main.java.srcDirs
    }

    artifacts {
        archives sourcesJar
    }
} else {
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }
}

bintray {
    user = "$System.env.BINTRAY_USER"
    key = "$System.env.BINTRAY_KEY"
    if (project.hasProperty("android")) {
        publications = ['AndroidLibrary']
    }
    configurations = ['archives']
    pkg {
        repo = 'redux'
        name = "${this.group}:${this.archivesBaseName}"
        publish = true
        override = true
        version {
            name = this.version
            released = new Date()
            vcsTag = this.version
        }
    }
}
